;; 1. Based on: 
;; 2. Description: PK model
;; 3. Label:
;; x1. Author: user

$PROB  REPLICATE OF ADVAN2 USING DIFF EQUATIONS-- POPULATION DATA
$INPUT      ID TIME CP=DV DOSE=AMT 
$DATA       test_data.txt
$SUBROUTINES  ADVAN6  TOL=7    ;TOL - THE NUMBER OF ACCURATE DIGITS
$MODEL 
	COMP =(DEPOT,DEFDOSE) 
	COMP =(CENTRAL,DEFOBS,NOOFF)
$PK
 ;THETA(1)=MEAN ELIMINATION RATE CONSTANT K (1/HR) = 0.076
 ;THETA(2)=VOLUME OF DISTRIBUTION                  = 0.481
 ;THETA(3)=MEAN ABSORPTION RATE CONSTANT KA (1/HR) = 2.790
    CALLFL=1
    K= THETA(1)+ETA(1)   
    V= THETA(2)+ETA(2)  
    KA=THETA(3)+ETA(3)   ;ETA(1) - INTERINDIVIDUAL VARIABILITY OF KA (NU)
    ;CL=K*V
$THETA  (.001,.1,5) (.001,.5,20) (.001,3,10) 
$OMEGA BLOCK(3)  .1   .0001 .01   .0001 .0001 .1  ;VARIANCE OF ETA
$DES
    DADT(1)=-KA*A(1)
    DADT(2)= KA/V*A(1)-K*A(2)    ;A(2) = CP
$ERROR
    Y=F+EPS(1)

$SIGMA  .3                                    ;VARIANCE OF EPS     

$EST     MAXEVAL=450  PRINT=5
$COV                          ;COVARIANCE STEP IS IMPLEMENTED
$TABLE          ID TIME DOSE PRED ETA1 ETA2 ETA3 FILE=tab001 NOPRINT
$SCAT           (DV PRED) VS TIME BY ID
$TABLE ID TIME DV PRED FILE=tab1 NOPRINT ONEHEADER

